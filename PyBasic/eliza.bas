5 PRINT TAB ( 12 ) ; "**************************" 
10 PRINT TAB ( 22 ) ; "ELIZA" 
20 PRINT TAB ( 16 ) ; "CREATIVE COMPUTING" 
30 PRINT TAB ( 14 ) ; "MORRISTOWN, NEW JERSEY" : PRINT 
40 PRINT TAB ( 15 ) ; "ADAPTED FOR IBM PC BY" 
50 PRINT TAB ( 8 ) ; "PATRICIA DANIELSON AND PAUL HASHFIELD" 
53 PRINT : PRINT TAB ( 2 ) ; "PLEASE DON'T USE COMMAS OR PERIODS IN YOUR INPUTS" : PRINT 
55 PRINT TAB ( 12 ) ; "*************************" 
60 PRINT : PRINT : PRINT 
80 REM*****INITIALIZATION**********
100 DIM S ( 36 ) :dim R ( 36 ) :dim N ( 36 ) : DIM KEYLEN(10)
105 DIM KEYWORD$ ( 28 ):DIM KWINDX(28):dim WORDIN$ ( 8 ):dim WORDOUT$ ( 8 ):DIM KEYWORD2$(5):DIM KWINDX2(5)
107 REM NEED TO PRE-DECLARE BEFORE USE
108 P$ = "NULL"
110 N1 = 28 : N2 = 16 : N4 = 36: N5 = 5: KEYLEN(1) = 1:X1 = 1
111 FOR X = 1 TO N1
112 READ TEMP$,TEMP: KEYWORD$ ( X ) = TEMP$: KWINDX(X) = TEMP: IF LEN(TEMP$) <= X1 THEN 114
113 X1 = LEN(TEMP$):KEYLEN(X1) = X
114 NEXT X 
115 FOR X = 1 TO N2 / 2
116 READ TEMP$: WORDIN$ ( X ) = TEMP$ : READ TEMP$: WORDOUT$ ( X ) = TEMP$
117 NEXT X 
120 RESTORE 2530 
130 FOR X = 1 TO N4 
140 READ TEMP: S ( X ) = TEMP: READ TEMP: L = TEMP
142 R ( X ) = S ( X ) : N ( X ) = S ( X ) + L - 1 
150 NEXT X 
160 PRINT "HI! I'M ELIZA. WHAT'S YOUR PROBLEM?" 
170 REM *********************************** 
180 REM *******USER INPUT SECTION********** 
190 REM *********************************** 
200 INPUT I$ 
201 I$ = "  " + upper$(I$) + "  " 
210 REM GET RID OF APOSTROPHES 
220 FOR L = 1 TO LEN ( I$ ) 
230 REM IF MID$(I$,L,1)="'"THEN I$=MID$(I$, 1 ,L-1)+MID$(I$,L+1,LEN(I$)-L):GOTO 230 
240 IF L + 4 > LEN ( I$ ) THEN 243
241 IF MID$ ( I$ , L , 4 ) <> "SHUT" THEN 243
242 PRINT "O.K. IF YOU FEEL THAT WAY I'LL SHUT UP....":GOTO 246
243 IF L + 3 > LEN ( I$ ) THEN 250
244 IF MID$ ( I$ , L , 3 ) <> "BYE" THEN 250
245 PRINT "GOOD-BYE THEN...."
246 END 
250 NEXT L 
255 IF I$ = P$ THEN 256 ELSE 260
256 PRINT "PLEASE DON'T REPEAT YOURSELF!": GOTO 170
260 REM *********************************** 
270 REM ********FIND KEYWORD IN I$********* 
280 REM ***********************************
282 X = 0:K=0:TWOWORD=0:THREEWORD=0:WORD$="":ISTRT=1:IEND=4:C$=""
284 X = X + 1:FNDSPACE = 1:GOTO 288
286 WORD$ = WORD$ + MID$(I$,X,1):X=X+1:FNDSPACE = 0
288 IF X >= LEN(I$) THEN 370
290 IF FNDSPACE=1 AND MID$(I$,X,1) <> " " THEN 286
292 IF FNDSPACE=1 THEN 284
294 IF MID$(I$,X,1) <> " " THEN 286

296 REM FOUND NEW WORD TO CHECK
300 FOR K1 = KEYLEN(LEN(WORD$)) TO N1
301 IF LEN(KEYWORD$(K1)) > LEN(WORD$) THEN 350
340 IF WORD$ <> KEYWORD$ ( K1 ) THEN 349 
342 K = KWINDX(K1):F$ = WORD$
345 IF K<>13 AND K<>20 THEN GOTO 430
346 IF K<>20 THEN 350
347 THREEWORD=1:ISTRT=5:IEND=6:GOTO 369
348 GOTO 350 
349 NEXT K1

350 IF TWOWORD=0 THEN 359
351 FOR X1 = 1 TO TWOWORD
352 IF WORD$ <> KEYWORD2$(X1) THEN 356
353 IF THREEWORD=0 THEN 355
354 KEY2WORD$ = "WHY "+KEY2WORD$
355 K = KWINDX2(X1):F$ = KEY2WORD$ + " " + WORD$:GOTO 430
356 NEXT X1
357 TWOWORD = 0
358 IF K=0 THEN 369 ELSE 430

359 FOR K1 = ISTRT TO IEND
360 RESTORE 2639+K1:READ KEY2WORD$
361 IF WORD$ <> KEY2WORD$ THEN 367
362 READ TWOWORD
363 FOR X1 = 1 TO TWOWORD
364 READ TEMP$,TEMP:KEYWORD2$(X1)=TEMP$:KWINDX2(X1)=TEMP
365 NEXT X1
366 GOTO 369
367 NEXT K1
368 IF THREEWORD <> 0 THEN 430

369 WORD$ = "":GOTO 284
370 K = 36 : GOTO 570 : REM WE DIDN'T FIND ANY KEYWORDS 
380 REM ****************************************** 
390 REM **TAKE PART OF STRING AND CONJUGATE IT**** 
400 REM **USING THE LIST OF STRINGS TO BE SWAPPED* 
410 REM ****************************************** 
430 L = INSTR(I$,F$):C$ = " " + MID$ ( I$ , LEN(F$) + L ,LEN ( I$ ) - LEN ( F$ ) - L + 1 ) + " " 
435 X1 = 0:WORD$=""
440 X1 = X1 + 1:FNDSPACE = 1:GOTO 450
445 WORD$ = WORD$ + MID$(C$,X1,1):X1=X1+1:FNDSPACE = 0
450 IF X1 >= LEN(C$) THEN 470
455 IF FNDSPACE=1 AND MID$(C$,X1,1) <> " " THEN 445
460 IF FNDSPACE=1 THEN 440
465 IF MID$(C$,X1,1) <> " " THEN 445
470 REM FOUND NEW WORD TO CHECK
475 IF WORD$ = "" OR WORD$=" " THEN 551
480 L = X1 - LEN(WORD$)

485 FOR X = 1 TO N2/2
487 IF WORD$ <> WORDIN$ ( X ) THEN 520 
490 C$ = MID$ ( C$ , 1 , L - 1 ) + WORDOUT$ ( X ) + MID$ ( C$ , L+LEN(WORDIN$(X)) , LEN ( C$ ) - L - LEN ( WORDIN$ ( X ) ) + 1 ) 
500 X1 = X1 - LEN(WORDIN$(X)) + LEN(WORDOUT$(X)):WORD$="":GOTO 440 
520 IF WORD$ <> WORDOUT$ ( X ) THEN 540 
530 C$ = MID$ ( C$ , 1 , L - 1 ) + WORDIN$ ( X ) + MID$ ( C$ , L+LEN(WORDOUT$(X)) , LEN ( C$ ) - L - LEN ( WORDOUT$ ( X ) ) + 1 ) 
535 X1 = X1 - LEN(WORDOUT$(X)) + LEN(WORDIN$(X)):WORD$="":GOTO 440
540 NEXT X
545 WORD$="":GOTO 440
551 IF MID$ ( C$ , 2 , 1 ) = " " THEN 552 ELSE 555
552 C$ = MID$ ( C$ , 2 , LEN ( C$ ) - 1 ) : REM ONLY 1 SPACE 
554 REM REMOVE EXPLENATION MARKS?
555 REM FOR L = 1 TO LEN ( C$ ) 
556 REM IF MID$ ( C$ , L , 1 ) = "!" THEN 557 ELSE 558
557 REM C$ = MID$ ( C$ , 1 , L - 1 ) + MID$ ( C$ , L+1 , LEN ( C$ ) - L ) : GOTO 556 
558 REM NEXT L
560 REM ********************************************** 
570 REM **NOW USING THE KEYWORD NUMBER (K) GET REPLY** 
580 REM **********************************************
600 RESTORE 1320+R(K):READ F$
610 R ( K ) = R ( K ) + 1
615 IF R ( K ) > N ( K ) THEN 617 ELSE 620
617 R ( K ) = S ( K ) 
620 IF MID$ ( F$ , LEN(F$) , 1 ) <> "*" THEN 621 ELSE 625
621 PRINT F$ : P$ = I$ : GOTO 170 
625 IF C$ <> "   " THEN 630 
626 PRINT "YOU WILL HAVE TO ELABORATE MORE FOR ME TO HELP YOU" 
627 GOTO 170 
630 PRINT MID$ ( F$ , 1 , LEN ( F$ ) - 1 ) ; C$ 
640 P$ = I$ : GOTO 170 
1000 REM ******************************* 
1010 REM *****PROGRAM DATA FOLLOWS****** 
1020 REM ******************************* 
1030 REM *********KEYWORDS************** 
1049 REM ******************************* 
1050 DATA "NO", 28 , "HI", 26
1060 DATA "I'M", 12, "YOU" , 13, "HOW", 16 , "WHO", 17 , "YES", 33 , "WHY", 20 
1070 DATA "WHAT", 15 , "NAME", 21 , "YOUR", 29 , "WHEN", 19 
1080 DATA "WHERE", 18 , "SORRY", 23 , "DREAM", 24 , "HELLO", 25 , "THINK", 31 , "ALIKE", 32 , "MAYBE", 27 
1090 DATA "ALWAYS", 30 , "YOU'RE", 4, "FRIEND", 34 , "DREAMS", 24
1100 DATA "BECAUSE", 22 , "FRIENDS", 34, "COMPUTER", 35 , "COMPUTERS", 35, "NOKEYFOUND", 36 
1200 REM ********************************* 
1210 REM ***STRING DATA FOR CONJUGATIONS** 
1220 REM ********************************* 
1230 DATA "ARE" , "AM" , "WERE" , "WAS" , "YOU" , "ME" , "YOUR" , "MY" 
1235 DATA "I'VE" , "YOU'VE" , "I'M" , "YOU'RE" 
1240 DATA "ME" , "YOU", "I", "YOU"
1300 REM ****************************** 
1310 REM *********REPLIES************** 
1320 REM ****************************** 
1321 DATA "DON'T YOU BELIEVE THAT I CAN*" 
1322 DATA "PERHAPS YOU WOULD LIKE TO BE LIKE ME*" 
1323 DATA "YOU WANT ME TO BE ABLE TO*" 
1324 DATA "PERHAPS YOU DON'T WANT TO*" 
1325 DATA "DO YOU WANT TO BE ABLE TO*" 
1326 DATA "WHAT MAKES YOU THINK I AM*" 
1327 DATA "DOES IT PLEASE YOU TO BELIEVE I AM*" 
1328 DATA "PERHAPS YOU WOULD LIKE TO BE*" 
1329 DATA "DO YOU SOMETIMES WISH YOU WERE*" 
1330 DATA "DON'T YOU REALLY*" 
1331 DATA "WHY DON'T YOU*" 
1332 DATA "DO YOU WISH TO BE ABLE TO*" 
1333 DATA "DOES THAT TROUBLE YOU*" 
1334 DATA "DO YOU OFTEN FEEL*" 
1335 DATA "DO YOU OFTEN FEEL*" 
1336 DATA "DO YOU ENJOY FEELING*" 
1337 DATA "DO YOU REALLY BELIEVE I DON'T*" 
1338 DATA "PERHAPS IN GOOD TIME I WILL*" 
1339 DATA "DO YOU WANT ME TO*" 
1340 DATA "DO YOU THINK YOU SHOULD BE ABLE TO*" 
1341 DATA "WHY CAN'T YOU*" 
1342 DATA "WHY ARE YOU INTERESTED IN WHETHER OR NOT I AM*" 
1343 DATA "WOULD YOU PREFER IF I WERE NOT*" 
1344 DATA "PERHAPS IN YOUR FANTASIES I AM*" 
1345 DATA "HOW DO YOU KNOW YOU CAN'T*" 
1346 DATA "HAVE YOU TRIED?" 
1347 DATA "PERHAPS YOU CAN NOW*" 
1348 DATA "DID YOU COME TO ME BECAUSE YOU ARE*" 
1349 DATA "HOW LONG HAVE YOU BEEN*" 
1350 DATA "DO YOU BELIEVE IT IS NORMAL TO BE*" 
1351 DATA "DO YOU ENJOY BEING*" 
1352 DATA "WE WERE DISCUSSING YOU--NOT ME." 
1353 DATA "OH, I*" 
1354 DATA "YOU'RE NOT REALLY TALKING ABOUT ME, ARE YOU?" 
1355 DATA "WHAT WOULD IT MEAN TO YOU IF YOU GOT*" 
1356 DATA "WHY DO YOU WANT*" 
1357 DATA "SUPPOSE YOU SOON GOT*" 
1358 DATA "WHAT IF YOU NEVER GOT*" 
1359 DATA "I SOMETIMES ALSO WANT*" 
1360 DATA "WHY DO YOU ASK?" 
1361 DATA "DOES THAT QUESTION INTEREST YOU?" 
1362 DATA "WHAT ANSWER WOULD PLEASE YOU THE MOST?" 
1363 DATA "WHAT DO YOU THINK?" 
1364 DATA "ARE SUCH QUESTIONS ON YOUR MIND OFTEN?" 
1365 DATA "WHAT IS IT THAT YOU REALLY WANT TO KNOW?" 
1366 DATA "HAVE YOU ASKED ANYONE ELSE?" 
1367 DATA "HAVE YOU ASKED SUCH QUESTIONS BEFORE?" 
1368 DATA "WHAT ELSE COMES TO MIND WHEN YOU ASK THAT?" 
1369 DATA "NAMES DON'T INTEREST ME." 
1370 DATA "I DON'T CARE ABOUT NAMES --PLEASE GO ON." 
1371 DATA "IS THAT THE REAL REASON?" 
1372 DATA "DON'T ANY OTHER REASONS COME TO MIND?" 
1373 DATA "DOES THAT REASON EXPLAIN ANYTHING ELSE?" 
1374 DATA "WHAT OTHER REASONS MIGHT THERE BE?" 
1375 DATA "PLEASE DON'T APOLOGIZE!" 
1376 DATA "APOLOGIES ARE NOT NECESSARY." 
1377 DATA "WHAT FEELINGS DO YOU HAVE WHEN YOU APOLOGIZE?" 
1378 DATA "DON'T BE SO DEFENSIVE!" 
1379 DATA "WHAT DOES THAT DREAM SUGGEST TO YOU?" 
1380 DATA "DO YOU DREAM OFTEN?" 
1381 DATA "WHAT PERSONS APPEAR IN YOUR DREAMS?" 
1382 DATA "ARE YOU DISTURBED BY YOUR DREAMS?" 
1383 DATA "HOW DO YOU DO ...PLEASE STATE YOUR PROBLEM." 
1384 DATA "YOU DON'T SEEM QUITE CERTAIN." 
1385 DATA "WHY THE UNCERTAIN TONE?" 
1386 DATA "CAN'T YOU BE MORE POSITIVE?" 
1387 DATA "YOU AREN'T SURE?" 
1388 DATA "DON'T YOU KNOW?" 
1389 DATA "ARE YOU SAYING NO JUST TO BE NEGATIVE?" 
1390 DATA "YOU ARE BEING A BIT NEGATIVE." 
1391 DATA "WHY NOT?" 
1392 DATA "ARE YOU SURE?" 
1393 DATA "WHY NO?" 
1394 DATA "WHY ARE YOU CONCERNED ABOUT MY*" 
1395 DATA "WHAT ABOUT YOUR OWN*" 
1396 DATA "CAN YOU THINK OF A SPECIFIC EXAMPLE?" 
1397 DATA "WHEN?" 
1398 DATA "WHAT ARE YOU THINKING OF?" 
1399 DATA "REALLY, ALWAYS?" 
1400 DATA "DO YOU REALLY THINK SO?" 
1401 DATA "BUT YOU ARE NOT SURE*" 
1402 DATA "DO YOU DOUBT*" 
1403 DATA "IN WHAT WAY?" 
1404 DATA "WHAT RESEMBLANCE DO YOU SEE?" 
1405 DATA "WHAT DOES THE SIMILARITY SUGGEST TO YOU?" 
1406 DATA "WHAT OTHER CONNECTIONS DO YOU SEE?" 
1407 DATA "COULD THERE REALLY BE SOME CONNECTION?" 
1408 DATA "HOW?" 
1409 DATA "YOU SEEM QUITE POSITIVE." 
1410 DATA "ARE YOU SURE?" 
1411 DATA "I SEE." 
1412 DATA "I UNDERSTAND." 
1413 DATA "WHY DO YOU BRING UP THE TOPIC OF FRIENDS?" 
1414 DATA "DO YOUR FRIENDS WORRY YOU?" 
1415 DATA "DO YOUR FRIENDS PICK ON YOU?" 
1416 DATA "ARE YOU SURE YOU HAVE ANY FRIENDS?" 
1417 DATA "DO YOU IMPOSE ON YOUR FRIENDS?" 
1418 DATA "PERHAPS YOUR LOVE FOR FRIENDS WORRIES YOU." 
1419 DATA "DO COMPUTERS WORRY YOU?" 
1420 DATA "ARE YOU TALKING ABOUT ME IN PARTICULAR?" 
1421 DATA "ARE YOU FRIGHTENED BY MACHINES?" 
1422 DATA "WHY DO YOU MENTION COMPUTERS?" 
1423 DATA "WHAT DO YOU THINK MACHINES HAVE TO DO WITH YOUR PROBLEM?" 
1424 DATA "DON'T YOU THINK COMPUTERS CAN HELP PEOPLE?" 
1425 DATA "WHAT IS IT ABOUT MACHINES THAT WORRIES YOU?" 
1426 DATA "SAY, DO YOU HAVE ANY PSYCHOLOGICAL PROBLEMS?" 
1427 DATA "WHAT DOES THAT SUGGEST TO YOU?" 
1428 DATA "I SEE." 
1429 DATA "I'M NOT SURE I UNDERSTAND YOU FULLY." 
1430 DATA "COME COME ELUCIDATE YOUR THOUGHTS." 
1431 DATA "CAN YOU ELABORATE ON THAT?" 
1432 DATA "THAT IS QUITE INTERESTING." 
2500 REM ************************* 
2510 REM *****DATA FOR FINDING RIGHT REPLIES 
2520 REM ************************* 
2530 DATA 1 , 3 , 4 , 2 , 6 , 4 , 6 , 4 , 10 , 4 , 14 , 3 , 17 , 3 , 20 , 2 , 22 , 3 , 25 , 3 
2540 DATA 28 , 4 , 28 , 4 , 32 , 3 , 35 , 5 , 40 , 9 , 40 , 9 , 40 , 9 , 40 , 9 , 40 , 9 , 40 , 9 
2550 DATA 49 , 2 , 51 , 4 , 55 , 4 , 59 , 4 , 63 , 1 , 63 , 1 , 64 , 5 , 69 , 5 , 74 , 2 , 76 , 4 
2560 DATA 80 , 3 , 83 , 7 , 90 , 3 , 93 , 6 , 99 , 7 , 106 , 6 
2600 REM ********************************
2620 REM *** MULTI WORD KEYWORD FLAGS ***
2630 REM ********************************
2640 DATA "CAN",2,"YOU",1,"I",2
2641 DATA "YOU",1,"ARE",3
2642 DATA "I",5,"DON'T",5,"FEEL",6,"CAN'T",10,"AM",11,"WANT",14
2643 DATA "ARE",1,"YOU",9
2644 DATA "DON'T",1,"YOU",7
2645 DATA "CAN'T",1,"I",8
2680 REM THREE WORD KEYWORD FLAG
2690 DATA "WHY",2,"DON'T","YOU",7,"CAN'T","I",8

